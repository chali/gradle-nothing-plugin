plugins {
    id "nebula.plugin-plugin" version "9.1.2"
    id 'java-gradle-plugin'
}

description 'Gradle plugins to do nothing' // nebula-plugin-plugin sets this as the pluginBundle description

dependencies {
    compile 'com.google.collections:google-collections:1.+' // to verify '1.+' resolves to static version
    compile 'commons-logging:commons-logging:latest.release' // to verify 'latest.release' resolves to static version
}

// Specify the details of each plugin
gradlePlugin {
    plugins {
        nothingPlugin {
            id = 'nebula.nothing-plugin'
            displayName = 'Gradle nothing plugin'
            description = "Plugin that does nothing"
            implementationClass = 'nebula.plugin.nothing.NothingPlugin'
        }
        moreNothingPlugin {
            id = 'nebula.more-nothing-plugin'
            displayName = 'Gradle more nothing plugin'
            description = "Another plugin that does nothing"
            implementationClass = 'nebula.plugin.nothing.MoreNothingPlugin'
        }
    }
}

// These settings are set for the whole plugin bundle
pluginBundle {
    // nebula-plugin-plugin sets the website, vcsUrl, and description

    // tags and description can be set for the whole bundle here, but can also
    // be set / overridden in the config for specific plugins
    tags = ['nebula', 'nothing', 'example']
}

contacts {
    'justin@halfempty.org' {
        moniker 'Justin Ryan'
        github 'Justin Ryan'
        role 'Developer'
    }
    '2860150+OdysseusLives@users.noreply.github.com' {
        github 'OdysseusLives'
    }
}

publishing {
    repositories {
        maven {
            url = "$buildDir/maven-repo"
        }
    }
    publications {
        // publish with a customized POM
        pluginMaven(MavenPublication) {
            pom {
                name = 'Gradle plugins to do nothing - with customized POM'
                inceptionYear = '2014'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}